plugins {
    id 'war'
    id 'idea'
    id 'org.gretty' version '2.2.0'
}

configurations {
    copyOnly
}

repositories {
    jcenter()
}

dependencies {
    providedCompile 'javax.servlet:javax.servlet-api:4.0.1'
    copyOnly 'com.github.jsimone:webapp-runner:9.0.8.1'
    copyOnly 'org.apache.tomcat.embed:tomcat-embed-websocket:9.0.8'
    compile 'com.google.code.gson:gson:2.8.5'
    compile 'me.atrox.haikunator:Haikunator:1.3'
    compile 'org.tinylog:tinylog:1.3.5'
    compile 'com.github.tobiasrm:tinylog-coloredconsole:1.3.1'
    compile fileTree(dir: 'lib', include: '*.jar')
}

idea {
    module {
        outputDir file('build/classes/main')
    }
}

task copyWebappRunner {
    doLast {
        copy {
            from(configurations.copyOnly) {
                include 'tomcat-embed-websocket*'
                include 'webapp-runner*'
            }
            into "$buildDir/libs"
        }
        copy {
            from 'src/main/build'
            into "$buildDir/libs"
        }
    }
}

build.mustRunAfter ':client:build'
build.mustRunAfter ':kernel:build'
build.dependsOn ':kernel:buildServer', 'war', 'copyWebappRunner'

task run {
    dependsOn 'build'
    doLast {
        exec {
            if (org.gradle.internal.os.OperatingSystem.current().isWindows()) {
                commandLine "$buildDir/libs/variED.bat"
            } else {
                commandLine "$buildDir/libs/variED.sh"
            }
            workingDir = file("$buildDir/libs")
        }
    }
}