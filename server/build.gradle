buildscript {
    repositories {
        maven { url "http://repository.jsweet.org/artifactory/libs-snapshot-local" }
        maven { url "http://repository.jsweet.org/artifactory/plugins-snapshot-local" }
    }
    dependencies {
        classpath('org.jsweet:jsweet-gradle-plugin:2.2.0-SNAPSHOT') { transitive = true }
    }
}

plugins {
    id 'war'
    id 'idea'
    id 'org.gretty' version '2.2.0'
}

apply plugin: 'org.jsweet.jsweet-gradle-plugin'

configurations {
    copyOnly
}

repositories {
    jcenter()
    maven { url "http://repository.jsweet.org/artifactory/libs-snapshot-local" }
}

dependencies {
    providedCompile 'javax.servlet:javax.servlet-api:4.0.1'
    copyOnly 'com.github.jsimone:webapp-runner:9.0.8.1'
    copyOnly 'org.apache.tomcat.embed:tomcat-embed-websocket:9.0.8'
    compile 'com.google.code.gson:gson:2.8.5'
    compile 'me.atrox.haikunator:Haikunator:1.3'
    compile 'org.jsweet:jsweet-core:6-SNAPSHOT'
    compile fileTree(dir: 'lib', include: '*.jar')
}

gretty {
    httpPort = 8080
    contextPath = '/'
}

idea {
    module {
        outputDir file('build/classes/main')
    }
}

jsweet {
    includes = ['de/ovgu/spldev/varied/common/**/*.java']
    outDir = file('../client/src/common')
    tsOut = file('.jsweet/ts')
    workingDir = file('.jsweet')
    declaration = true
    noRootDirectories = true
    module = 'es2015'
}

task copyWebappRunner {
    doLast {
        copy {
            from(configurations.copyOnly) {
                include 'tomcat-embed-websocket*'
                include 'webapp-runner*'
            }
            into "$buildDir/libs"
        }
        copy {
            from 'src/main/build'
            into "$buildDir/libs"
        }
    }
}

build.mustRunAfter ':client:build'
build.dependsOn 'war', 'copyWebappRunner'
task run(dependsOn: 'appRun')

// As of now, this has to be run MANUALLY if any common classes changed.
task transpileCommon(dependsOn: 'jsweet')

task cleanCommon {
    doLast {
        delete fileTree(dir: '../client/src/common')
    }
}

clean.dependsOn 'cleanCommon'